# Maintainer: David Spink <yorper_protonmail.com>

# Contributor: Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Helmut Stult <helmut[at]manjaro[dot]org>

_linuxprefix=linux54
_extramodules=extramodules-5.4-CLEANJARO
pkgbase=$_linuxprefix-spl_zfs
pkgname=("$_linuxprefix-zfs")
pkgver=0.8.2
pkgrel=13
url="http://zfsonlinux.org/"
arch=('x86_64')
license=('CDDL' 'GPLv2')
depends=("$_linuxprefix" "kmod")
makedepends=("$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
install=install
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz"
        https://github.com/zfsonlinux/zfs/commit/e5db31349484e5e859c7a942eb15b98d68ce5b4d.patch)
sha256sums=('47608e257c8ecebb918014ef1da6172c3a45d990885891af18e80f5cc28beab8'
            '486b9b04795037ff9d38533a7e51f412ac6372ba3da8fccebb96af4ecc812eaf')

prepare() {
  cd "${srcdir}/zfs-${pkgver}"
  # patches here

  # Linux 5.0 compat: SIMD compatibility
  patch -Np1 -i ../e5db31349484e5e859c7a942eb15b98d68ce5b4d.patch
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}/zfs-${pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --libdir=/usr/lib \
              --datadir=/usr/share --includedir=/usr/include --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-${pkgver} --with-config=kernel \
 	      --with-linux=/usr/lib/modules/${_kernver}/build \
	      --with-linux-obj=/usr/lib/modules/${_kernver}/build
  make
}

package_linux54-zfs(){
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("zfs=$pkgver")
  depends+=("zfs-utils=${pkgver}")
  replaces=('linux51-spl<=0.7.13')

  cd "${srcdir}/zfs-${pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}
